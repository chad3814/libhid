#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_REVISION($Revision: 1.21 $)
AC_COPYRIGHT([Please see the ./COPYING file for license information.])

AC_PREREQ(2.57)

##### INITIALISATION #########################################################

# Making releases:
#     if functions were added, arg1 = 0
#     if not backward compatible, arg1 = arg2 = 0
#
m4_define([IFACE_AGE], [0])
m4_define([BIN_AGE],   [0])
m4_define([MD_INIT_NAME], m4_esyscmd([set -- `head -1 README` && echo -n $1]))
m4_define([MD_INIT_VERSION], m4_esyscmd([head -1 debian/changelog | sed -e 's,.*(\([^-]*\)-[0-9]*).*,\1,' | tr -d "\n"]).IFACE_AGE.BIN_AGE)
m4_define([MD_INIT_ADDRESS], m4_esyscmd([grep -i bugreports\ to README | sed -e 's,.*<\([^>]*\).*,\1,' | tr -d "\n"]))

AC_INIT(MD_INIT_NAME, MD_INIT_VERSION, MD_INIT_ADDRESS)

AC_CONFIG_HEADER([config.h])
AC_CONFIG_SRCDIR([include/hid.h])
AC_CONFIG_AUX_DIR([.])

AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE

##### CONFIGURE ARGUMENTS ####################################################

MD_ARG_ENABLE_CUSTOM(pedantic, pedantic compiler checks, yes)
MD_ARG_ENABLE_CUSTOM(warnings, proper compiler warnings, yes)
MD_ARG_ENABLE_CUSTOM(werror, compiler warnings as errors, no)
MD_ARG_ENABLE_CUSTOM(debug, debugging, no)
MD_ARG_ENABLE_CUSTOM(gdbdebug, gdb-enhanced debugging symbols, no)

MD_ARG_WITH_CUSTOM(ccache, ccache for compiler caching, [yes, if found])
MD_ARG_WITH_CUSTOM(distcc, distcc for distributed compilation, [yes, if found])
MD_ARG_WITH_CUSTOM(doxygen, documentation generated by doxygen, [yes, if found])

# TODO: move to with
MD_ARG_ENABLE_CUSTOM(swig, swig python wrapper, yes)

##### VERSIONING #############################################################

MD_VERSION_INFO
MD_LT_VERSION_INFO

PACKAGE=AC_PACKAGE_NAME
VERSION=$MD_VERSION

##### CONFIGURATION ##########################################################

MD_CHECK_OS

MD_CHECK_LIBUSB018B

AC_PROG_CC
MD_CHECK_GCC3

MD_CONF_DISTCC
MD_CONF_CCACHE

AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_HEADER_STDC
AC_HEADER_STDBOOL

AC_C_INLINE
AC_C_CONST
AC_C_BIGENDIAN
AC_C_STRINGIZE

MD_CONF_DEBUGGING
MD_CONF_COMPILER
AM_CONDITIONAL(OS_LINUX, test "$MD_OS" = "linux")
AM_CONDITIONAL(OS_BSD, test "$MD_OS" = "bsd")
AM_CONDITIONAL(OS_DARWIN, test "$MD_OS" = "darwin")

MD_CHECK_SWIG([1.3])
MD_CHECK_DOXYGEN

MD_CONF_FLAGS(
  [-I../include -I../hidparser],
  [$OS_CFLAGS $LIBUSB_CFLAGS],
  [],
  [$OS_LDFLAGS $LIBUSB_LIBS]
)

##### OUTPUT #################################################################

AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([src/Makefile])
AC_CONFIG_FILES([hidparser/Makefile])
AC_CONFIG_FILES([swig/Makefile])
AC_CONFIG_FILES([test/Makefile])
AC_CONFIG_FILES([doc/doxygen.rc])
AC_CONFIG_FILES([doc/Makefile])
AC_CONFIG_FILES([doc/www/Makefile])
AC_CONFIG_FILES([pkgconfig/libhid.pc])

AC_OUTPUT

MD_DEBUG_BUILDVARS

# COPYRIGHT --
#
# This file is part of libhid, a user-space HID access library.
# libhid is (c) 2003-2004
#   Martin F. Krafft <libhid@pobox.madduck.net>
#   Charles Lepple <clepple@ghz.cc>
#   Arnaud Quette <arnaud.quette@free.fr> && <arnaud.quette@mgeups.com>
# and distributed under the terms of the GNU General Public License.
# See the file ./COPYING in the source distribution for more information.
#
# THIS PACKAGE IS PROVIDED "AS IS" AND WITHOUT ANY EXPRESS OR IMPLIED
# WARRANTIES, INCLUDING, WITHOUT LIMITATION, THE IMPLIED WARRANTIES
# OF MERCHANTIBILITY AND FITNESS FOR A PARTICULAR PURPOSE.

